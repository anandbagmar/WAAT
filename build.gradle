apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def title = 'WAAT'
version = 'v2.0'
group = "com.github.anandbagmar"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile 'commons-collections:commons-collections:3.2.2'
    compile 'commons-lang:commons-lang:2.6'
    compile 'log4j:log4j:1.2.17'
    compile 'com.thoughtworks.xstream:xstream:1.3.1'
    compile 'net.lightbody.bmp:browsermob-core:2.1.5'
    compile 'org.seleniumhq.selenium:selenium-java:3.141.59'
    compile group: 'io.appium', name: 'java-client', version: '7.0.0'

    testCompile 'org.testng:testng:6.14.3'
    testCompile 'org.uncommons:reportng:1.1.4'
    testCompile 'org.apache.velocity:velocity:1.7'
    testCompile 'velocity-tools:velocity-tools:2.0-beta1'
    compile 'io.github.bonigarcia:webdrivermanager:3.7.1'
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

jar {
    manifest {
        attributes 'Implementation-Title': title,
                'Implementation-Version': version
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': title,
                'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

uploadArchives {
    repositories {
        flatDir {
            name '*.jar'
            dirs 'dist'
        }
    }
}

task copyJars(type: Copy) {
    from('build/libs')
    into('dist')
    include('*.jar')
}
