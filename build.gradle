buildscript {
    ext {
        gradleVersion = '7.1'
    }
}

plugins {
    id "org.sonarqube" version "3.3"
    id "java"
    id "idea"
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def title = 'WAAT'
version = 'v2.0'
group = "com.github.anandbagmar"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "commons-collections:commons-collections:3.2.2"
    implementation "log4j:log4j:1.2.17"
    implementation "com.thoughtworks.xstream:xstream:1.3.1"
    implementation "net.lightbody.bmp:browsermob-core:2.1.5"
    implementation "org.seleniumhq.selenium:selenium-java:3.141.59"
    implementation "io.appium:java-client:7.0.0"
    implementation "org.testng:testng:7.4.0"
    implementation "io.github.bonigarcia:webdrivermanager:+"
}

test {
    useTestNG()
}

jar {
    manifest {
        attributes 'Implementation-Title': title,
                'Implementation-Version': version
    }
}

shadowJar {
    archiveBaseName.set("$project.name")
    archiveClassifier.set('')
    archiveVersion.set("$project.version")
    zip64 true
    manifest {
            attributes 'Implementation-Title': title,
                    'Implementation-Version': archiveVersion
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

wrapper {
    gradleVersion = project.gradleVersion // version from gradle.properties
}

publishing {
    publications {
        testing(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

task copyJars(type: Copy) {
    from('build/libs')
    into('dist')
    include('*.jar')
}

wrapper {
    gradleVersion = project.gradleVersion // version from gradle.properties
}
